plugins {
	id 'fabric-loom' version '1.5-SNAPSHOT'
	id 'maven-publish'
	id "me.modmuss50.mod-publish-plugin" version "0.5.1"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

// Add minecraft version to the build jar name
archivesBaseName = project.archives_base_name + '-' + project.minecraft_version
version = project.mod_version
group = project.maven_group

loom {
	accessWidenerPath = file("src/main/resources/skyblockmod.accesswidener")
}

repositories {
	maven { url "https://maven.terraformersmc.com/releases/" }
	exclusiveContent {
		forRepository {
			maven {
				name = "Modrinth"
				url = "https://api.modrinth.com/maven"
			}
		}
		filter {
			includeGroup "maven.modrinth"
		}
	}
	maven { url "https://jitpack.io" }
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

	modCompileOnly "maven.modrinth:skyblocker-liap:v${project.skyblocker_version}+${project.minecraft_version}"

	include modImplementation("com.github.0x3C50:Renderer:${project.renderer_version}") {
		exclude group: "io.github.ladysnake" exclude module: "satin"
	}

	include modImplementation("org.eclipse.jgit:org.eclipse.jgit:${project.jgit_version}")

	// Apache Commons Math
	include implementation("org.apache.commons:commons-math3:${project.commons_math_version}")

	testImplementation "net.fabricmc:fabric-loader-junit:${project.loader_version}"
}

test {
	useJUnitPlatform()
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}

publishMods {
	file = remapJar.archiveFile
	displayName = "Skyblock Mod ${mod_version} for ${minecraft_version}"
	changelog = rootProject.file("CHANGELOG.md").text
	type = STABLE
	modLoaders.add("fabric")

	curseforge {
		projectId = "658094"
		projectSlug = "skyblockmod" // Required for discord webhook
		accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
		minecraftVersions.add("1.20.3")
		minecraftVersions.add("1.20.4")
		requires("fabric-api")
	}
	modrinth {
		projectId = "GTIrlvJk"
		accessToken = providers.environmentVariable("MODRINTH_TOKEN")
		minecraftVersions.add("1.20.3")
		minecraftVersions.add("1.20.4")
	}
	github {
		repository = "kevinthegreat1/SkyblockMod-Fabric"
		accessToken = providers.environmentVariable("GITHUB_TOKEN")
		commitish = "main"
	}
}
